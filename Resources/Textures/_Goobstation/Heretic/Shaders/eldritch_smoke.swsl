//
// Description : Array and textureless GLSL 2D simplex noise function.
//      Author : Ian McEwan, Ashima Arts.
//  Maintainer : stegu
//     Lastmod : 20110822 (ijm)
//     License : Copyright (C) 2011 Ashima Arts. All rights reserved.
//               Distributed under the MIT License. See LICENSE file.
//               https://github.com/ashima/webgl-noise
//               https://github.com/stegu/webgl-noise
//
highp vec3 mod289(highp vec3 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}
highp vec2 mod289(highp vec2 x) {
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}
highp vec3 permute(highp vec3 x) {
  return mod289(((x*34.0)+10.0)*x);
}
highp float snoise(highp vec2 v)
  {
  const highp vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                      0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                     -0.577350269189626,  // -1.0 + 2.0 * C.x
                      0.024390243902439); // 1.0 / 41.0
// First corner
  highp vec2 i  = floor(v + dot(v, C.yy) );
  highp vec2 x0 = v -   i + dot(i, C.xx);
// Other corners
  highp vec2 i1;
  //i1.x = step( x0.y, x0.x ); // x0.x > x0.y ? 1.0 : 0.0
  //i1.y = 1.0 - i1.x;
  i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
  // x0 = x0 - 0.0 + 0.0 * C.xx ;
  // x1 = x0 - i1 + 1.0 * C.xx ;
  // x2 = x0 - 1.0 + 2.0 * C.xx ;
  highp vec4 x12 = x0.xyxy + C.xxzz;
  x12.xy -= i1;
// Permutations
  i = mod289(i); // Avoid truncation effects in permutation
  highp vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
		+ i.x + vec3(0.0, i1.x, 1.0 ));
  highp vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
  m = m*m ;
  m = m*m ;
// Gradients: 41 points uniformly over a line, mapped onto a diamond.
// The ring size 17*17 = 289 is close to a multiple of 41 (41*7 = 287)
  highp vec3 x = 2.0 * fract(p * C.www) - 1.0;
  highp vec3 h = abs(x) - 0.5;
  highp vec3 ox = floor(x + 0.5);
  highp vec3 a0 = x - ox;
// Normalise gradients implicitly by scaling m
// Approximation of: m *= inversesqrt( a0*a0 + h*h );
  m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
// Compute final noise value at P
  highp vec3 g;
  g.x  = a0.x  * x0.x  + h.x  * x0.y;
  g.yz = a0.yz * x12.xz + h.yz * x12.yw;
  return 130.0 * dot(m, g);
}


highp float fbm(highp vec2 uv, lowp int octaves) {
    highp float value = 0.0;
    highp float amplitude = 0.5;
    highp float frequency = 2.0;

    for (int i = 0; i < octaves; i++) {
        value += amplitude * snoise(uv * frequency);
        frequency *= 2.0;
        amplitude *= 0.5;
    }

    return value;
}

highp vec4 createSmoke(highp vec2 uv, highp float time) {
    highp float smoke1 = fbm(uv * 16.0 + vec2(0.0, time * 0.2), 4);
    highp float smoke2 = fbm(uv * 12.0 + vec2(time * 0.5), 3);
    highp float smoke3 = fbm(uv * 8.0 + vec2(time * 0.3), 2);

    highp float smokePattern = smoke1 + smoke2 + smoke3;

    smokePattern = clamp(smokePattern * 16.0, 0.0, 1.0);

    highp float gapPattern = fbm(uv * 32.0 + vec2(time * 0.5), 3);
    gapPattern = smoothstep(0.2, 0.0, gapPattern);

    highp float finalSmoke = smokePattern * gapPattern;

    highp float pulse = 0.5 + 0.5 * sin(time * 0.5);
    finalSmoke *= (0.8 + 0.2 * pulse);

    highp vec3 colorVariation = vec3(
        0.6 + 0.4 * sin(uv.x * 5.0 + time),
        0.6 + 0.4 * sin(uv.y * 5.0 + time * 1.2),
        0.6 + 0.4 * sin((uv.x + uv.y) * 3.0 + time * 0.8)
    );

    highp vec3 finalColor = colorVariation * finalSmoke;

    return vec4(finalColor, finalSmoke);
}

void fragment() {
    highp vec4 sprite = zTexture(UV);

    if (sprite.a == 0.0) {
        discard;
    }

    highp vec2 uv = FRAGCOORD.xy * SCREEN_PIXEL_SIZE;

    highp vec4 smoke = createSmoke(uv, TIME);

    COLOR = clamp(sprite * smoke, 0.0, 1.0);
}
