light_mode unshaded;

uniform lowp float pixelSize;

//
// https://gamedev.stackexchange.com/a/59808
//
//   Author: sam hocevar
// Answered: Jul 27, 2013 at 13:33
//  License: CC BY-SA 3.0
//

highp vec3 rgb2hsv(highp vec3 c) {
    highp vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    highp vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    highp vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));

    highp float d = q.x - min(q.w, q.y);
    highp float e = 0.00000000010; //because this doesn't support doing 1.0e-10
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

highp vec3 hsv2rgb(highp vec3 c) {
    highp vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    highp vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}


highp float getNeighbor(highp vec2 uv, lowp int x, lowp int y) {
    lowp float xdif = float(x) / pixelSize;
    lowp float ydif = float(y) / pixelSize;
    highp vec2 res = vec2(uv.x + xdif, uv.y + ydif);
    if (res.x < 0.0 || res.x > 1.0 || res.y < 0.0 || res.y > 1.0)
        return 0.0;

    return zTexture(res).a;
}

void fragment() {
	highp vec4 sprite = zTexture(UV);
	if (sprite.a == 0.0) {
		discard;
	}
    highp float s = 0.0;
    for(int y = -2; y <= 2; y++) {
        for(int x = -2; x <= 2; x++) {
            s = s + getNeighbor(UV, x, y) * 0.4;
        }
    }
    highp vec3 hsv = rgb2hsv(sprite.xyz);
    sprite.rgb = hsv2rgb(vec3(pow(hsv.z, 0.5), s, 1.0));
    // sprite.rgb = sprite.rgb * vec3(1.0, 1.0, 0.5);

    COLOR = sprite;
}
